/*
 * Copyright 2020 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.android.compileSdk
    buildToolsVersion versions.android.buildTools

    defaultConfig {
        minSdkVersion versions.android.minSdk
        targetSdkVersion versions.android.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    flavorDimensions "version"

    productFlavors {
        gms {
            dimension "version"
        }

        standalone {
            dimension "version"
        }
    }
}

task copyGmsToStandalone {
    inputs.dir 'src/gms/java/com/google/maps/android/ktx'
    outputs.dir 'src/standalone/java/com/google/maps/android/ktx'
    doLast {
        copy {
            from 'src/gms/java/com/google/maps/android/ktx'
            into 'src/standalone/java/com/google/maps/android/ktx'
            filter { line ->
                line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
            }
        }
    }
}

preBuild.dependsOn copyGmsToStandalone

dependencies {
    api deps.kotlin
    implementation deps.kotlinxCoroutines

    // GMS Flavor specific dependencies
    gmsImplementation deps.playServices.maps

    // V3-Beta specific dependencies
    standaloneImplementation deps.playServices.basement
    standaloneImplementation deps.playServices.base
    standaloneImplementation deps.playServices.gcm
    standaloneImplementation deps.playServices.location
    standaloneImplementation name: 'maps-sdk-3.0.0-beta', ext: 'aar'

    // Tests
    testImplementation deps.playServices.maps
    testImplementation deps.androidx.test
    testImplementation deps.androidx.junit
    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.mockitoKotlin
}
