/*
 * Copyright 2020 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.android.compileSdk
    buildToolsVersion versions.android.buildTools

    defaultConfig {
        minSdkVersion versions.android.minSdk
        targetSdkVersion versions.android.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
}

/**
 * This task will copy all of the source code in this module into the `maps-v3-ktx` module
 * and change its imports from the Maps SDK on Play Services to the Standalone V3 Beta Maps SDK.
 */
task copyGmsToV3BetaSource {
    inputs.dir 'src/main/java/com/google/maps/android/ktx'
    outputs.dir '../src/main/java/com/google/maps/android/ktx'
    doLast {
        copy {
            from 'src/main/java/com/google/maps/android/ktx'
            into '../maps-v3-ktx/src/main/java/com/google/maps/android/ktx'
            filter { line ->
                line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
            }
        }
    }
}

/**
 * This task will copy all of the tests in this module into the `maps-v3-ktx` module
 * and change its imports from the Maps SDK on Play Services to the Standalone V3 Beta Maps SDK.
 */
task copyGmsToV3BetaTest {
    inputs.dir 'src/test/java/com/google/maps/android/ktx'
    outputs.dir '../src/main/java/com/google/maps/android/ktx'
    doLast {
        copy {
            from 'src/test/java/com/google/maps/android/ktx'
            into '../maps-v3-ktx/src/test/java/com/google/maps/android/ktx'
            filter { line ->
                line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
            }
        }
    }
}

preBuild.dependsOn(copyGmsToV3BetaSource, copyGmsToV3BetaTest)

dependencies {
    api deps.kotlin
    implementation deps.kotlinxCoroutines
    implementation deps.playServices.maps

    // Tests
    testImplementation deps.androidx.test
    testImplementation deps.androidx.junit
    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.mockitoKotlin
}
