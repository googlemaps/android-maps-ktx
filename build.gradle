/**
 * Copyright 2023 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.versions = [
        'android'          : [
            "buildTools": "30.0.3",
            "compileSdk": 34,
            "minSdk"    : 19,
            "targetSdk" : 34
        ],
        'androidMapsUtils' : '3.7.0',
        'androidx'         : [
            'appcompat': '1.1.0',
            'coreKtx'  : '1.2.0',
            'espresso' : '3.2.0',
            'test'     : '1.2.0',
            'junit'    : '1.1.1',
        ],
        'junit'            : '4.12',
        'kotlin'           : '1.8.21',
        'kotlinxCoroutines': '1.6.0',
        'mockito'          : '3.0.0',
        'mockitoKotlin'    : '2.2.0',
        'androidMapsSdk'     : '18.2.0',
        'volley'           : '1.2.0',
    ]

    ext.deps = [
        'androidMapsUtils' : [
            'gms': "com.google.maps.android:android-maps-utils:$versions.androidMapsUtils"
        ],
        'androidx'         : [
            'appcompat': "androidx.appcompat:appcompat:$versions.androidx.appcompat",
            'coreKtx'  : "androidx.core:core-ktx:$versions.androidx.coreKtx",
            'espresso' : "androidx.test.espresso:espresso-core:$versions.androidx.espresso",
            'test'     : "androidx.test:core:$versions.androidx.test",
            'junit'    : "androidx.test.ext:junit:$versions.androidx.junit"
        ],
        'junit'            : "junit:junit:$versions.junit",
        'kotlin'           : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin",
        'kotlinxCoroutines': "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlinxCoroutines",
        'mockito'          : "org.mockito:mockito-core:$versions.mockito",
        'mockitoKotlin'    : "com.nhaarman.mockitokotlin2:mockito-kotlin:$versions.mockitoKotlin",
        'playServices'     : [
            'maps' : "com.google.android.gms:play-services-maps:$versions.androidMapsSdk",
        ],
        'volley'           : "com.android.volley:volley:$versions.volley",
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.9.10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.mxalbert.gradle:jacoco-android:0.2.1'
        classpath "com.google.android.libraries.mapsplatform.secrets-gradle-plugin:secrets-gradle-plugin:2.0.1"
    }
}

ext.projectArtifactId = { project ->
    if (project.name == 'maps-utils-ktx' ||
        project.name == 'maps-ktx') {
        return project.name
    } else {
        return null
    }
}

/**
 * Shared configs across subprojects
 */
allprojects {
    group = 'com.google.maps.android'
    version = '4.0.0'
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)

    repositories {
        google()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
}

/**
 * Publishing and signing info
 */
subprojects { project ->
    if (project.ext.artifactId == null) return

    apply plugin: 'com.android.library'
    apply plugin: 'com.mxalbert.gradle.jacoco-android'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'signing'

    // Documentation
    tasks.named("dokkaHtml") {
        outputDirectory.set(file("${buildDir}/documentation"))
        dokkaSourceSets {
            configureEach {
                skipDeprecated.set(true)
                jdkVersion.set(8)
                skipEmptyPackages.set(true)
                reportUndocumented.set(true)
                displayName.set("JVM")
            }
        }
    }

    // Code coverage
    jacoco {
        toolVersion = "0.8.7"
    }


    tasks.withType(Test).configureEach {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    tasks.register('sourcesJar', Jar) {
        from android.sourceSets.main.java.source
        archiveClassifier = "sources"
    }

    tasks.register('dokkaJar', Jar) {
        dependsOn dokkaHtml
        from dokkaHtml.outputDirectory
        archiveClassifier = "javadoc"
    }

    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                pom {
                    name = project.name
                    description = "Kotlin extensions (KTX) for Google Maps SDK"
                    url = "https://github.com/googlemaps/android-maps-ktx"
                    scm {
                        connection = 'scm:git@github.com:googlemaps/android-maps-ktx.git'
                        developerConnection = 'scm:git@github.com:googlemaps/android-maps-ktx.git'
                        url = 'https://github.com/googlemaps/android-maps-ktx'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    organization {
                        name = 'Google Inc'
                        url = 'http://developers.google.com/maps'
                    }

                    developers {
                        developer {
                            name = 'Google Inc.'
                        }
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }

                afterEvaluate {
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                    artifact dokkaJar
                    artifact sourcesJar
                }
            }
        }

        repositories {
            maven {
                name = "mavencentral"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.aar
    }
}


tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

