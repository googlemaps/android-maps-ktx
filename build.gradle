/**
 * Copyright 2023 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.versions = [
            'android'          : [
                    "buildTools": "30.0.3",
                    "compileSdk": 34,
                    "minSdk"    : 21,
                    "targetSdk" : 34
            ]]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.gradle
        classpath libs.dokkaGradlePlugin
        classpath libs.kotlinGradlePlugin
        classpath libs.jacocoAndroid
        classpath libs.secretsGradlePlugin
    }
}

ext.projectArtifactId = { project ->
    if (project.name == 'maps-utils-ktx' ||
        project.name == 'maps-ktx') {
        return project.name
    } else {
        return null
    }
}

/**
 * Shared configs across subprojects
 */
allprojects {
    group = 'com.google.maps.android'
    version = '5.0.0'
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)

    repositories {
        google()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
}

/**
 * Publishing and signing info
 */
subprojects { project ->
    if (project.ext.artifactId == null) return

    apply plugin: 'com.android.library'
    apply plugin: 'com.mxalbert.gradle.jacoco-android'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'signing'

    // Documentation
    tasks.named("dokkaHtml") {
        outputDirectory.set(file("${buildDir}/documentation"))
        dokkaSourceSets {
            configureEach {
                skipDeprecated.set(true)
                jdkVersion.set(8)
                skipEmptyPackages.set(true)
                reportUndocumented.set(true)
                displayName.set("JVM")
            }
        }
    }

    // Code coverage
    jacoco {
        toolVersion = "0.8.7"
    }


    tasks.withType(Test).configureEach {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    tasks.register('sourcesJar', Jar) {
        from android.sourceSets.main.java.source
        archiveClassifier = "sources"
    }

    tasks.register('dokkaJar', Jar) {
        dependsOn dokkaHtml
        from dokkaHtml.outputDirectory
        archiveClassifier = "javadoc"
    }

    android {
        publishing {
            singleVariant("release") {
                withSourcesJar()
                withJavadocJar()
            }
        }
    }

    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                pom {
                    name = project.name
                    description = "Kotlin extensions (KTX) for Google Maps SDK"
                    url = "https://github.com/googlemaps/android-maps-ktx"
                    scm {
                        connection = 'scm:git@github.com:googlemaps/android-maps-ktx.git'
                        developerConnection = 'scm:git@github.com:googlemaps/android-maps-ktx.git'
                        url = 'https://github.com/googlemaps/android-maps-ktx'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    organization {
                        name = 'Google Inc'
                        url = 'http://developers.google.com/maps'
                    }

                    developers {
                        developer {
                            name = 'Google Inc.'
                        }
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }

                afterEvaluate {
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                    artifact dokkaJar
                    artifact sourcesJar
                }
            }
        }

        repositories {
            maven {
                name = "mavencentral"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username sonatypeToken
                    password sonatypeTokenPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.aar
    }
}


tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

